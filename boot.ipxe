#!ipxe
#
#Location for ipxe, http://boot.ipxe.org/
#Location for wimboot, https://git.ipxe.org/releases/wimboot/
#
chain secrets || goto error

# Distribution release version
set debian_name buster
set debian_release 10.8.0
set fedora_release 33
set opensuse_release 15.3
set ubuntu_release 20.10
set ubuntu_name groovy
set windows_release 10

# Mirrors
set arch_mirror mirror.csclub.uwaterloo.ca/archlinux
set debian_mirror debian.mirror.iweb.ca
set debian_live debian.mirror.iweb.ca
set fedora_mirror fedora.mirror.iweb.ca
#set fedora_mirror mirror.csclub.uwaterloo.ca/fedora
set opensuse_mirror mirror.csclub.uwaterloo.ca/opensuse
set ubuntu_mirror ubuntu.mirror.iweb.ca

# Some menu defaults
set spaces2:hex 20:20
set spaces2 ${spaces2:string}
set spaces5:hex 20:20:20:20:20
set spaces5 ${spaces5:string}
set spaces15:hex 20:20:20:20:20:20:20:20:20:20:20:20:20:20:20
set spaces15 ${spaces15:string}
set esc:hex 1b
set reset ${esc:string}[0m
set bold ${esc:string}[1m
set ital ${esc:string}[3m
set under ${esc:string}[4m
set boldoff ${esc:string}[22m
set italoff ${esc:string}[23m
set underoff ${esc:string}[24m
set fg_bla ${esc:string}[30m
set fg_red ${esc:string}[31m
set fg_gre ${esc:string}[32m
set fg_yel ${esc:string}[33m
set fg_blu ${esc:string}[34m
set fg_mag ${esc:string}[35m
set fg_cya ${esc:string}[36m
set fg_whi ${esc:string}[37m
set bg_bla ${esc:string}[40m
set bg_red ${esc:string}[41m
set bg_gre ${esc:string}[42m
set bg_yel ${esc:string}[43m
set bg_blu ${esc:string}[44m
set bg_mag ${esc:string}[45m
set bg_cya ${esc:string}[46m
set bg_whi ${esc:string}[47m
set cls ${esc:string}[2J
set menu-timeout 30000
set submenu-timeout ${menu-timeout}
#console --x 800 --y 600
#cpair --foreground 7 --background 0 0
isset ${menu-default} || set menu-default exit

# Figure out if client is 64-bit capable
iseq ${buildarch} i386 && set arch x86 || set arch x64
iseq ${buildarch} i386 && set archb 32 || set archb 64
iseq ${buildarch} i386 && set arch_fedora i386 || set arch_fedora x86_64
iseq ${buildarch} i386 && set arch_debian i386 || set arch_debian amd64
iseq ${platform} efi && set archp efi || set archp bios

#
# Main menu
:start
menu iPXE ${version} - ${manufacturer} ${product} - ${archb}bit ${archp}
item --key w win            ${bold}W${boldoff}indows ${windows_release} install
item --key a winauto        Windows ${windows_release} ${bold}a${boldoff}uto install
item --key l winlite        Windows ${windows_release} ${bold}l${boldoff}ite auto install
item --key b barebones      Windows ${windows_release} ${bold}b${boldoff}arebones auto install
item --key e winre          Windows ${windows_release} ${bold}e${boldoff}mergency recovery
item
item --key 1 arch           1. Arch linux
item --key 2 fedoraai       2. Fedora ${fedora_release} auto install
item --key 3 debianai       3. Debian ${debian_release} auto install
#item 
item --key g gparted        ${bold}G${boldoff}raphical Partition Manager
item --key z clone          Clone${bold}z${boldoff}illa
item --key k krd            ${bold}K${boldoff}aspersky Rescue Disk
item --key y systemrescue   S${bold}y${boldoff}stem Rescue
#item 
item --gap --               ------------------------------ Advanced ---------------------------------
item --key r refresh_menu   ${bold}R${boldoff}efresh menu
item config                 Configure settings
item shell                  Enter iPXE shell
item --key s efishell       EFI ${bold}s${boldoff}hell
item --key t reboot         Reboo${bold}t${boldoff}
item --key x --default exit E${bold}x${boldoff}it (boot local disk)
item --key o menu_other
choose --timeout ${menu-timeout} selected || goto cancel
set menu-timeout 0
goto ${selected}

########## UTILITY ITEMS ####################
:config
config
goto start

:shell
echo Type exit to get the back to the menu
shell
set menu-timeout 0
goto start

:reboot
reboot

:refresh_menu
chain -ar boot.ipxe

:failed
echo Booting failed, dropping to shell
goto shell

:cancel
echo You cancelled the menu, dropping you to a shell
goto shell

:back
set submenu-timeout 0
clear submenu-default
goto start

:exit
exit

#
:win
imgfree
kernel wimboot
initrd --name winpeshl.ini ${swboot-url}/pxe/unattend/win/install_winpeshl.ini  winpeshl.ini
goto winboot

:winauto
imgfree
kernel wimboot
initrd --name winpeshl.ini ${swboot-url}/pxe/unattend/win/unattend_winpeshl.ini winpeshl.ini
goto winboot

:winlite
imgfree
kernel wimboot
initrd --name winpeshl.ini ${swboot-url}/pxe/unattend/win/lite_winpeshl.ini winpeshl.ini
goto winboot

:barebones
imgfree
kernel wimboot
initrd --name winpeshl.ini ${swboot-url}/pxe/unattend/win/barebones_winpeshl.ini winpeshl.ini
goto winboot

:winre
imgfree
kernel wimboot
initrd --name winpeshl.ini ${swboot-url}/pxe/unattend/win/repair_winpeshl.ini   winpeshl.ini
goto winboot

:winboot
initrd --name bcd          ${swboot-url}/win/iso/expanded/10/boot/bcd           bcd
initrd --name boot.sdi     ${swboot-url}/win/iso/expanded/10/boot/boot.sdi      boot.sdi
initrd --name boot.wim     ${swboot-url}/win/iso/expanded/10/sources/boot.wim   boot.wim
boot

:arch
imgfree
set url  http://${arch_mirror}/iso/latest
set boot ${url}/arch/boot/${arch_fedora}
set ks   ${swboot-url}/pxe/unattend/lnx/arch/docker_install.sh
set ks   ${swboot-url}/pxe/unattend/lnx/arch/install.sh
initrd   ${boot}/initramfs-linux.img || goto failed
boot     ${boot}/vmlinuz-linux initrd=initramfs-linux.img \
         archiso_http_srv=${url}/ archisobasedir=arch \
         script=${ks} ip=dhcp console=tty0 console=tty1 \
         dist_mirror=http://${arch_mirror} || goto failed

:fedoraai
imgfree
set url http://${fedora_mirror}/linux/releases/${fedora_release}/Everything/${arch_fedora}/os
set upd http://${fedora_mirror}/linux/updates/${fedora_release}/Everything/${arch_fedora}
initrd  ${swboot-url}/pxe/misc/fedora/net/initrd.img || goto failed
boot    ${swboot-url}/pxe/misc/fedora/net/vmlinuz initrd=initrd.img \
        inst.repo=${url} inst.stage2=${url} inst.ks=${swboot-url}/pxe/unattend/lnx/fedora/ks.cfg \
        ks_url=${swboot-url}/pxe/unattend/lnx/fedora upd_url=${upd} net.ifnames=0 audit=0 || goto failed

:debianai
imgfree
set url http://${debian_mirror}/debian/dists/testing/main/installer-${arch_debian}/current/images/netboot/debian-installer/${arch_debian}
initrd  ${swboot-url}/pxe/misc/debian/net/initrd.gz || goto failed
boot    ${swboot-url}/pxe/misc/debian/net/linux initrd=initrd.gz \
        mirror/http/directory=/debian mirror/http/hostname=${debian_mirror} \
        url=${swboot-url}/pxe/unattend/lnx/debian/preseed.cfg \
        priority=critical ipv6.disable=1 net.ifnames=0 audit=0 auto=true || goto failed

:ubuntuai
imgfree
set url http://${ubuntu_mirror}/ubuntu/dists/${ubuntu_release}/main/installer-${arch_debian}/current/legacy-images/netboot/ubuntu-installer/${arch_debian}
initrd  ${url}/initrd.gz || goto failed
boot    ${url}/linux initrd=initrd.gz \
        mirror/http/hostname=${ubuntu_mirror} \
        url=${swboot-url}/pxe/unattend/lnx/ubuntu/preseed.cfg \
        priority=critical ipv6.disable=1 auto=true || goto failed

:efishell
# https://github.com/tianocore/edk2/tree/master/ShellBinPkg
imgfree
chain   ${swboot-url}/pxe/Shell.efi || imgfree

:gparted
imgfree
initrd  ${swboot-url}/pxe/misc/gparted/initrd.img || goto failed
boot    ${swboot-url}/pxe/misc/gparted/vmlinuz initrd=initrd.img \
        fetch=${swboot-url}/pxe/misc/gparted/filesystem.squashfs boot=live union=overlay username=user vga=788 \
        config components noeject noswap locales=en_US.UTF-8 keyboard-layouts=en || goto failed
#        fetch=${swboot-url}/pxe/misc/gparted/filesystem.squashfs boot=live union=overlay username=user ip=${dns} vga=788 \
#        gl_batch config components noeject noprompt noswap locales=en_US.UTF-8 keyboard-layouts=en || goto failed

:clone
imgfree
initrd  ${swboot-url}/pxe/misc/clonezilla/initrd.img || goto failed
boot    ${swboot-url}/pxe/misc/clonezilla/vmlinuz initrd=initrd.img \
        fetch=${swboot-url}/pxe/misc/clonezilla/filesystem.squashfs \
        union=overlay config components noswap username=user edd=on nomodeset nodmraid locales=en_US.UTF-8 keyboard-layouts=en \
        boot=live ocs_live_run="ocs-live-general" ocs_live_extra_param="" ocs_live_batch=no \
        net.ifnames=0 nosplash noprompt || goto failed

:krd
imgfree
initrd  ${swboot-url}/pxe/misc/krd/boot/grub/initrd.xz || goto failed
boot    ${swboot-url}/pxe/misc/krd/boot/grub/k-x86_64 initrd=initrd.xz \
        nfsboot=${nfs-url}/pxe/misc/krd lang=en dostartx || goto failed

:systemrescue
imgfree
initrd  ${swboot-url}/pxe/misc/systemrescue/sysresccd/boot/${arch_fedora}/sysresccd.img || goto failed
boot    ${swboot-url}/pxe/misc/systemrescue/sysresccd/boot/${arch_fedora}/vmlinuz initrd=sysresccd.img \
        archiso_http_srv=${swboot-url}/pxe/misc/systemrescue/ archisobasedir=sysresccd \
        checksum=y ip=dhcp net.ifnames=0 console=tty0 console=tty1 || goto failed

#
# Other menu
:menu_other
#colour --basic 6 --rgb 0x40e0d0 6
menu Other OS installation
item --key 1 debian         ${spaces15}${spaces15}${spaces5}${bg_gre}${fg_bla}Debian ${debian_release} ${debian_name}${boldoff}
item --key 2 debianlive     ${spaces15}1. Install${spaces15}${spaces15}2. live
item --gap
item --key 3 fedora         ${spaces15}${spaces15}${spaces5}Fedora ${fedora_release}
item --key 4 fedoralive     ${spaces15}3. Install${spaces15}${spaces15}4. live
item --gap
item --key 5 rawhide        ${spaces15}${spaces15}${spaces5}Fedora Rawhide
item --key 6 rawhidelive    ${spaces15}5. Install${spaces15}${spaces15}6. live
item --gap
item --key 7 ubuntuserver   ${spaces15}${spaces15}${spaces5}Ubuntu ${ubuntu_release} ${ubuntu_name}
item --key 8 ubuntuwrkstn  ${spaces15}7. Install${spaces15}${spaces15}8. live
item --gap
item --key 9 tumbleweed     ${spaces15}${spaces15}${spaces5}OpenSuse Install
item --key 0 opensuse       ${spaces15}9. Tumbleweed${spaces15}${spaces5}${spaces5}${spaces2}0. Leap ${opensuse_release}
item --gap
item --key a alpine         A. Alpine linux
item --key b alpine         B. Arch linux
item --key z test           Z. Test
item --gap
item --key 0x08 --default back        <== Back to top menu...
choose selected && goto ${selected} || goto start

:alpine
imgfree
set url http://alpinelinux.mirror.iweb.ca/edge
initrd  ${url}/releases/x86_64/netboot/initramfs-lts || goto failed
boot    ${url}/releases/x86_64/netboot/vmlinuz-lts initrd=initramfs-lts \
        modloop=${url}/releases/x86_64/netboot/modloop-lts alpine_repo=${url}/main \
        modules=loop,squashfs quiet nomodeset console=tty0 || goto failed

:debian
imgfree
#set url http://${debian_mirror}/debian/dists/Debian${debian_release}/main/installer-${arch_debian}/current/images/netboot/debian-installer/${arch_debian}
set url http://${debian_mirror}/debian/dists/bullseye/main/installer-${arch_debian}/current/images/netboot/debian-installer/${arch_debian}
initrd  ${url}/initrd.gz || goto failed
boot    ${url}/linux initrd=initrd.gz \
        mirror/http/directory=/debian mirror/http/hostname=${debian_mirror} mirror/country=manual || goto failed

:debianlive
imgfree
initrd  ${swboot-url}/pxe/misc/debian/initrd.img || goto failed
boot    ${swboot-url}/pxe/misc/debian/vmlinuz initrd=initrd.img \
        fetch=${swboot-url}/pxe/misc/debian/filesystem.squashfs \
        boot=live || goto failed

:fedora
imgfree
set url http://${fedora_mirror}/linux/releases/${fedora_release}/Everything/${arch_fedora}/os
initrd  ${url}/images/pxeboot/initrd.img || goto failed
boot    ${url}/images/pxeboot/vmlinuz initrd=initrd.img inst.repo=${url} devfs=nomount || goto failed

:fedoralive
imgfree
initrd  ${swboot-url}/pxe/misc/fedora/initrd.img || goto failed
boot    ${swboot-url}/pxe/misc/fedora/vmlinuz initrd=initrd.img \
        root=live:${swboot-url}/pxe/misc/fedora/squashfs.img \
        ro ip=dhcp rd.live.image rd.lvm=0 rd.luks=0 rd.md=0 rd.dm=0 || goto failed

:rawhide
imgfree
set url http://${fedora_mirror}/linux/development/rawhide/Everything/${arch_fedora}/os
initrd  ${url}/images/pxeboot/initrd.img || goto failed
boot    ${url}/images/pxeboot/vmlinuz initrd=initrd.img inst.repo=${url} devfs=nomount || goto failed

:rawhidelive
imgfree
initrd  ${swboot-url}/pxe/misc/rawhide/initrd.img || goto failed
boot    ${swboot-url}/pxe/misc/rawhide/vmlinuz initrd=initrd.img \
        root=live:${swboot-url}/pxe/misc/rawhide/squashfs.img \
        ro ip=dhcp rd.live.image rd.lvm=0 rd.luks=0 rd.md=0 rd.dm=0 || goto failed

:opensuse
imgfree
set url http://${opensuse_mirror}/distribution/leap/${opensuse_release}/repo/oss
initrd  ${url}/boot/${arch_fedora}/loader/initrd || goto failed
boot    ${url}/boot/${arch_fedora}/loader/linux initrd=initrd install=${url} || goto failed

:tumbleweed
imgfree
set url http://provo_mirror.opensuse.org/tumbleweed/repo/oss
initrd  ${url}/boot/${arch_fedora}/loader/initrd || goto failed
boot    ${url}/boot/${arch_fedora}/loader/linux initrd=initrd install=${url} || goto failed

:ubuntuserver
imgfree
initrd  ${swboot-url}/pxe/misc/ubuntu/initrdserver || goto failed
boot    ${swboot-url}/pxe/misc/ubuntu/vmlinuzserver initrd=initrdserver \
        url=${swboot-url}/pxe/misc/ubuntu/ubuntuserver.iso \
        ethdevice-timeout=30 ip=dhcp root=/dev/ram0 ramdisk_size=5000000 || goto failed

:ubuntulegacy
imgfree
set url http://${ubuntu_mirror}/ubuntu/dists/${ubuntu_release}/main/installer-${arch_debian}/current/legacy-images/netboot/ubuntu-installer/${arch_debian}
initrd  ${url}/initrd.gz || goto failed
boot    ${url}/linux initrd=initrd.gz \
        mirror/http/directory=/ubuntu mirror/http/hostname=${ubuntu_mirror} mirror/country=manual || goto failed

:ubuntuwrkstn
imgfree
initrd  ${swboot-url}/pxe/misc/ubuntu/initrdworkstation || goto failed
boot    ${swboot-url}/pxe/misc/ubuntu/vmlinuzworkstation initrd=initrdworkstation \
        url=${swboot-url}/pxe/misc/ubuntu/ubuntuworkstation.iso \
        ethdevice-timeout=30 ip=dhcp root=/dev/ram0 ramdisk_size=5000000 || goto failed

:test
imgfree
chain tftp://10.13.1.5/bootx64.efi || goto failed
goto failed
